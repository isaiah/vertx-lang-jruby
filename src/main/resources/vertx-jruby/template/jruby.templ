@comment{"This is where the generation starts"}
@comment{"==================================="}

/*\n
 * Copyright 2014 Red Hat, Inc.\n
 *\n
 * Red Hat licenses this file to you under the Apache License, version 2.0\n
 * (the "License"); you may not use this file except in compliance with the\n
 * License.  You may obtain a copy of the License at:\n
 *\n
 * http://www.apache.org/licenses/LICENSE-2.0\n
 *\n
 * Unless required by applicable law or agreed to in writing, software\n
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT\n
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the\n
 * License for the specific language governing permissions and limitations\n
 * under the License.\n
 */\n\n

package @{ifacePackageName.replace("io.vertx.", "io.vertx.jruby.")};\n

@comment{'the current index of the cache'}
@code{cacheDecls=new java.util.ArrayList()}

@code{
    constructor = ifaceSimpleName;
    def methodParam(m) {
        methodList = methodsByName.get(m);
        ret = new java.util.LinkedHashSet();
        ret.add("ThreadContext context");
        if (st) {
            ret.add("IRubyObject recv");
        }
        overload = methodList.size() > 1;
        if (overload) {
            ret.add("IRubyObject[] args");
        } else {
            for (p : methodList.get(0).params) {
                ret.add("IRubyObject " + p.name);
            }
        }
        ret;
    }
    def methodAnno(name) {
        methodList = methodsByName.get(name);
        overload = methodList.size() > 1;
        ret = new java.util.HashMap();
        maxParamLength = 0;
        minParamLength = 0;
        for(method : methodList) {
            if (maxParamLength < method.params.size()) {
                maxParamLength = method.params.size();
            }
            if (minParamLength > method.params.size()) {
                minParamLength = method.params.size();
            }
        }
        required = minParamLength;
        optional = maxParamLength - required;
        ret["name"] = helper.convertCamelCaseToUnderscores(name);
        if (st) {
            ret["meta"] = true;
        }
        if (overload) {
            if (optional != 0) {
                ret["optional"] = optional;
            }
            if (required != 0) {
                ret["required"] = required;
            }
        }
        ret.entrySet();
    }
    def quote(val) {
        if (val instanceof String) {
            "\"" + val + "\"";
        } else {
            val;
        }
    }
    def isPriv(kind) {
        kind == CLASS_PRIMITIVE || kind == CLASS_BOXED_PRIMITIVE
    }
}

@declare{'jrubyanno'}
@@JRubyMethod(@foreach{field : f}@{field.key}=@{quote(field.value)}@end{', '})\n
@end{}

@declare{'jrubyParams'}
@foreach{field : methodParam(m)}@{field}@end{', '}
@end{}

@declare{'unboxParam'}
    @code{
        pt = param.type.kind;
        ptn = param.type.name;
        pn = param.name;
        if (overload) {
            pn = "args[" + i + "]";
        }
    }
    @if{isPriv(pt)}
        @if{ptn == 'int' || ptn == 'Integer'}
            RubyNumeric.num2int(@{pn})
        @else{ptn == 'long' || ptn == 'Long'}
            RubyNumeric.num2long(@{pn})
        @else{}
            @{param.name}.asJavaString()
        @end{}
    @else{}
        @{pn}
    @end{}
@end{}

@declare{'unboxParams'}
    @code{i = 0}
    @foreach{param: params}
        @includeNamed{'unboxParam'; p=param; overload=overload; i=i}
        @code{i++}
    @end{", "}
@end{}

@declare{"typeMapping"}
    @code{pt = t.kind; pn = t.name}
    @if{isPriv(t.kind)}
        @if{pn == "int" || pn == "Integer" || pn == "float" || pn == "Float"}
            RubyFixnum
        @else{}
            RubyString
        @end{}
    @else{}
        RubyString
    @end{}
@end{}

@declare{"invokeDelegate"}
    @if{methodEntry.getValue().size() == 1}
        @if{st}@{type.raw}@else{}this.delegate@end{}.@{method.name}(@includeNamed{'unboxParams'; params=method.params; overload=false});\n
    @else{}
        @foreach{method: methodEntry.getValue()}
            @if{method.params.size() > 0}
                @code{i = 0}
                if (
                @foreach{param : method.params}
                    args[@{i}] instanceof @includeNamed{"typeMapping"; t=method.params[i].type}
                    @code{i++}
                @end{" && "}
                ) {\n
                    @if{st}@{type.raw}@else{}this.delegate@end{}.@{method.name}(@includeNamed{'unboxParams'; params=method.params; overload=true});\n
                }\n
            @else{}
                @if{st}@{type.raw}@else{}this.delegate@end{}.@{method.name}();\n
            @end{}
        @end{}
    @end{}
@end{}

@declare{'boxRet'}
    return 
    @code{rt = method.returnType.kind; rtn = method.returnType.simpleName}
    @if{isPriv(rt)}
        @if{rtn == 'int' || rtn == 'Integer' || rtn == 'long' || rtn == 'Long'}
            context.runtime.newFixnum(ret)
        @else{rtn == 'float' || rtn == 'Float' || rtn == 'double' || rtn == 'Double'}
            context.runtime.newFloat(ret)
        @else{rtn == 'String'}
            context.runtime.newString(ret)
        @else{}
            ret
        @end{}
    @else{rt == CLASS_API}
        new Ruby@{rtn}(context.runtime, metaClass, ret)
    @else{}
        ret
    @end{}
    ;\n
@end{}

import org.jruby.Ruby;\n
import org.jruby.RubyClass;\n
import org.jruby.RubyModule;\n
import org.jruby.RubyObject;\n
import org.jruby.anno.JRubyClass;\n
import org.jruby.anno.JRubyMethod;\n
import org.jruby.runtime.ObjectAllocator;\n
import org.jruby.runtime.ThreadContext;\n
import org.jruby.runtime.builtin.IRubyObject;\n
\n
import @{ifacePackageName}.@{ifaceSimpleName};\n
@comment{"Generate the imports"}
@foreach{importedType:importedTypes}
    @if{importedType.kind == CLASS_API}
        @if{!ifacePackageName.equals(importedType.packageName)}
import @{importedType.toString().replace("io.vertx.", "io.vertx.jruby.")};\n
        @end{}
    @else{}
        @if{!importedType.packageName.equals("java.lang")}
import @{importedType};\n
        @end{}
    @end{}
@end{}

@if{ifaceComment != null}
/**\n
 *@{ifaceComment.replaceAll('\n(?!$)', '\n *')}
 */\n
@end{}
@JRubyClass(name = "@{ifaceSimpleName}")\n
public class Ruby@{helper.getSimpleName(ifaceFQCN)} extends RubyObject {\n
    private @{type.raw} delegate;\n
    @comment{"JRuby hooks"}
    public static void createRuby@{constructor}(final Ruby runtime) {\n
      RubyModule mVertx = runtime.getModule("Vertx");\n
      RubyClass c@{constructor} = mVertx.defineClassUnder("@{constructor}", runtime.getObject(), new ObjectAllocator() {\n
        @Override\n
        public RubyClass allocate(Ruby runtime, RubyClass klazz) {\n
          return new Ruby@{constructor}(runtime, klazz);\n
        }\n
      });\n
      c@{constructor}.defineAnnotatedMethod(Ruby@{constructor}.class);\n
    }\n
    public Ruby@{constructor}(Ruby runtime, RubyClass klazz) {\n
      super(runtime, klazz);\n
    }\n

    @comment{"Internal constructor"}
    protected Ruby@{constructor}(Ruby runtime, RubyClass klazz, @{constructor} delegate) {\n
      super(runtime, klazz);\n
      this.delegate = delegate;\n
    }\n

    @comment{"Generate ruby initializers"}
    @JRubyMethod(rest=true)\n
    public IRubyObject initialize(ThreadContext context, IRubyObject[] args) {\n
      this.delegate = new @{constructor}(args);\n
      return this;\n
    }\n

    public @{constructor} get@{constructor}() {\n
      return delegate;\n
    }\n

@comment{"Generate methods"}
@foreach{methodEntry:methodsByName.entrySet()}
    @code{method = methodEntry.getValue().get(0); st = method.staticMethod}
    @includeNamed{'jrubyanno'; f=methodAnno(methodEntry.getKey())}
    public @if{st}static @end{}IRubyObject @{method.name}(
@includeNamed{"jrubyParams"; m = methodEntry.getKey()}
) {\n
        @comment{"method invoke"}
        @if{!method.fluent && ! method.returnType.name.equals("void")}
            @{method.returnType.simpleName} ret;\n
            ret = @includeNamed{"invokeDelegate"; methodEntry=methodEntry;overload=false}
        @else{}
            @includeNamed{"invokeDelegate"; methodEntry=methodEntry}
        @end{}
        @comment{"dispatch return value"}
        @if{method.fluent}
            return this;\n
        @else{method.returnType.name.equals("void")}
            return context.runtime.getNil();\n
        @else{}
            @includeNamed{"boxRet";m=method}
        @end{}
    }\n
@end{}
@comment{"End of class definition"}
}\n

